entrypoints Module;

comment "--";
comment "{-" "-}";

-- These are tokens, not terminals, so we can recover source positions
position token PiKeyword    {"Π"};
position token LamKeyword   {"λ"};
position token SigmaKeyword {"Σ"};
position token TypeKeyword  {"Type"};

-- TODO: exclude \v (vertical tab)
position token Name (char - [" \t\r\n\f(),."])+;

layout toplevel;

Module. Module ::= [Decl];

Define. Decl ::= "define" Param "≡" Expr;
Assume. Decl ::= "assume" Name ":" Expr;
Prove.  Decl ::= "prove"  Name ":" Expr;

separator Decl ";";

Pair.   Expr  ::= Expr1 "," Expr;
Arrow.  Expr1 ::= Expr2 "→" Expr1;
Times.  Expr2 ::= Expr3 "×" Expr2;
Equals. Expr3 ::= Expr4 "=" Expr4;
App.    Expr4 ::= Expr4 Expr5;

-- Binders are ambiguous and cause shift/reduce conflicts (which should be
-- resolved by shifting).
Pi.     Expr5 ::= PiKeyword    Param "." Expr;
Lam.    Expr5 ::= LamKeyword   Param "." Expr;
Sigma.  Expr5 ::= SigmaKeyword Param "." Expr;

Id.     Expr6 ::= Name;
Type.   Expr6 ::= TypeKeyword;

coercions Expr 6;

UntypedParam. Param ::= Name;
TypedParam.   Param ::= Name ":" Expr;
