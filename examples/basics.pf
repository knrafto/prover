-- Identity types, with based path induction.
assume Id : Π A. A → A → Type
assume refl : Π A. Π a : A. a = a
assume Id-ind : Π A.
    Π C : (Π a : A. Π b : A. a = b → Type).
    (Π a. C a a (refl A a)) →
    Π a. Π b. Π p. C a b p
assume Id-ind-refl : Π A.
    Π C : (Π a : A. Π b : A. a = b → Type).
    Π c : Π a. C a a (refl A a).
    Π a.
    Id-ind A C c a a (refl A a) = c a

-- Empty type.
assume 0 : Type
assume 0-ind : Π C : 0 → Type. Π a. C a

-- Unit type.
assume 1 : Type
assume tt : 1
assume 1-ind : Π C : 1 → Type. C tt → Π a. C a
assume 1-ind-tt : Π C : 1 → Type. Π c : C tt. 1-ind C c tt = c

-- Boolean type.
assume 2 : Type
assume 0₂ : 2
assume 1₂ : 2
assume 2-ind : Π C : 2 → Type. C 0₂ → C 1₂ → Π a. C a
assume 2-ind-0₂ : Π C : 2 → Type. Π c₀ : C 0₂. Π c₁ : C 1₂. 2-ind C c₀ c₁ 0₂ = c₀
assume 2-ind-1₂ : Π C : 2 → Type. Π c₀ : C 0₂. Π c₁ : C 1₂. 2-ind C c₀ c₁ 1₂ = c₁

-- Natural numbers.
assume ℕ : Type
assume zero : ℕ
assume suc : ℕ → ℕ
assume ℕ-ind : Π C : ℕ → Type. C zero → (Π n. C n → C (suc n)) → Π n. C n
assume ℕ-ind-zero : Π C : ℕ → Type. Π base : C zero. Π step : (Π n. C n → C (suc n)). ℕ-ind C base step zero = base
assume ℕ-ind-suc : Π C : ℕ → Type. Π base : C zero. Π step : (Π n. C n → C (suc n)). Π n. ℕ-ind C base step (suc n) = step n (ℕ-ind C base step n)

-- Structure of identity types.
-- TODO: infer C of Id-ind in all cases?
define ap (A : Type) (B : Type) (x : A) (y : A) (f : A → B)
    : x = y → f x = f y
    ≡ Id-ind A (λ x. λ y. λ p. f x = f y) (λ x. refl B (f x)) x y
define subst (A : Type) (x : A) (y : A) (B : A → Type)
    : x = y → B x → B y
    ≡ Id-ind A (λ x. λ y. λ p. B x → B y) (λ x. λ b. b) x y
define sym (A : Type) (x : A) (y : A)
    : x = y → y = x
    ≡ Id-ind A (λ x. λ y. λ p. y = x) (λ x. refl A x) x y
define trans (A : Type) (x : A) (y : A) (z : A)
    : x = y → y = z → x = z
    ≡ λ p : x = y. λ q : y = z.
        Id-ind A (λ x. λ y : A. λ p : x = y. Π z : A. y = z → x = z) (λ x. λ z. λ p. p) x y p z q

-- Function types.
define happly (A : Type) (B : Type) (f : A → B) (g : A → B)
    : f = g → Π x : A. f x = g x
    ≡ Id-ind (A → B) (λ f. λ g. λ p. Π x : A. f x = g x) (λ f. λ x. refl B (f x)) f g

-- Sets and logic.
-- define IsContr (A : Type) ≡ Σ a : A. Π x : A. a = x
define IsProp (A : Type) ≡ Π x : A. Π y : A. x = y

-- Natural numbers.
define add
    : ℕ → ℕ → ℕ
    ≡ ℕ-ind _ (λ m. m) (λ n. λ rec. λ m. suc (rec m))
